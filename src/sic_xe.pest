WHITESPACE = _{ " " | "\t" }
COMMENT = _{ "." ~ ANY* ~ NEWLINE? }

Register = { "A" | "X" | "L" | "B" | "S" | "T" | "F" }
RegisterPair = ${ Register ~ "," ~ Register }
Integer = @{ "-"? ~ ASCII_DIGIT+ }
String = @{ "C'" ~ (!"'" ~ ANY)* ~ "'"  }
Byte = @{ "X'" ~ ASCII_HEX_DIGIT+ ~"'" }
Literal = { String | Integer | Byte }
Symbol = { ASCII_ALPHA+ }

Command = ${ ExtendedAddressingMode? ~ Symbol }
Operand = ${
	(ImmediateAddressingMode | IndirectAddressingMode)? ~
    (Literal  | RegisterPair | Symbol) ~
    IndexingAddressingMode?
}

ExtendedAddressingMode = { "+" }
ImmediateAddressingMode = { "#" }
IndirectAddressingMode = { "@" }
IndexingAddressingMode = { ",X" }

Expression = ${
    // label + command + operand
    Symbol ~ WHITESPACE+ ~ Command ~ WHITESPACE+ ~ Operand |
    // command + operand
    Command ~ WHITESPACE+ ~ Operand |
    // label + command
    Symbol ~ WHITESPACE+ ~ Command |
    // single command
    Command
}

Program = {
    SOI ~ NEWLINE* ~ (Expression ~ NEWLINE+)* ~ Expression? ~ EOI
}

